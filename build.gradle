plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'org.unbroken-dome.test-sets' version("4.1.0")
}
repositories {
    mavenCentral()
}

sourceCompatibility = '11'

dependencies {
    implementation group: 'net.java.dev.jna', name: 'jna', version: '5.15.0'
    implementation group: 'net.java.dev.jna', name: 'jna-platform', version: '5.15.0'

    compileOnly group: 'com.bloxbean.cardano', name: 'cardano-client-lib', version: '0.6.3'

    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.2.0'
    testImplementation('org.assertj:assertj-core:3.24.2')
    testRuntimeOnly 'org.slf4j:slf4j-log4j12:2.0.5'

    testImplementation group: 'com.bloxbean.cardano', name: 'cardano-client-lib', version: '0.6.3'
    testImplementation group: 'com.bloxbean.cardano', name: 'cardano-client-backend-koios', version: '0.6.3'
    testImplementation group: 'com.bloxbean.cardano', name: 'cardano-client-backend-blockfrost', version: '0.6.3'

    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "native"
        }
    }
}

testSets {
    integrationTest
}

tasks.withType(Test) {
    useJUnitPlatform()
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

integrationTest {
    description = 'Set system properties for integration tests'
    systemProperty('BF_PROJECT_ID', findProperty("BF_PROJECT_ID"))
}

//create a single Jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'com.bloxbean.cardano.client.cli.CLIMain'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test {
    useJUnitPlatform()
    testLogging {
        showStandardStreams = true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }
            pom {
                name = 'Aiken Java Binding'
                description = 'A Java binding for Aiken Smart Contract Language Library'
                url = 'https://github.com/bloxbean/aiken-java-binding'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'satran004'
                        name = 'Satya'
                    }
                    developer {
                        id = 'matiwinnetou'
                        name = 'Mateusz Czeladka'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/bloxbean/aiken-java-binding'
                    developerConnection = 'scm:git:ssh://git@github.com/bloxbean/aiken-java-binding'
                    url = 'https://github.com/bloxbean/aiken-java-binding'
                }
            }
        }
    }

    repositories {
        String ossrhUsername = System.getenv('MAVEN_USERNAME')
        String ossrhPassword = System.getenv('MAVEN_PASSWORD')

        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

if (isReleaseVersion && !project.hasProperty("skipSigning")) {
    signing {
//        sign configurations.archives
        sign publishing.publications
    }
}
